#!/usr/bin/env python
from misc.utility.scons_hints import *

Import("env")
Import("env_modules")

env_harfbuzz = env_modules.Clone()
env_harfbuzz.disable_warnings()

# Thirdparty source files

thirdparty_obj = []
freetype_enabled = "freetype" in env.module_list
text_server_adv_enabled = "text_server_adv" in env.module_list
icu_enabled = text_server_adv_enabled
graphite_enabled = freetype_enabled and text_server_adv_enabled and env["graphite"]

if env["builtin_harfbuzz"]:
    thirdparty_dir = "#thirdparty/harfbuzz/"
    thirdparty_sources = [
        "src/hb-aat-layout.cc",
        "src/hb-aat-map.cc",
        "src/hb-blob.cc",
        "src/hb-buffer-serialize.cc",
        "src/hb-buffer-verify.cc",
        "src/hb-buffer.cc",
        # "src/hb-cairo-utils.cc",
        # "src/hb-cairo.cc",
        "src/hb-common.cc",
        # "src/hb-coretext-font.cc",
        # "src/hb-coretext-shape.cc",
        # "src/hb-directwrite.cc",
        "src/hb-draw.cc",
        "src/hb-face-builder.cc",
        "src/hb-face.cc",
        "src/hb-fallback-shape.cc",
        "src/hb-font.cc",
        # "src/hb-gdi.cc",
        # "src/hb-glib.cc",
        # "src/hb-gobject-structs.cc",
        "src/hb-map.cc",
        "src/hb-number.cc",
        "src/hb-ot-cff1-table.cc",
        "src/hb-ot-cff2-table.cc",
        "src/hb-ot-color.cc",
        "src/hb-ot-face.cc",
        "src/hb-ot-font.cc",
        "src/hb-ot-layout.cc",
        "src/hb-ot-map.cc",
        "src/hb-ot-math.cc",
        "src/hb-ot-meta.cc",
        "src/hb-ot-metrics.cc",
        "src/hb-ot-name.cc",
        "src/hb-ot-shaper-arabic.cc",
        "src/hb-ot-shaper-default.cc",
        "src/hb-ot-shaper-hangul.cc",
        "src/hb-ot-shaper-hebrew.cc",
        "src/hb-ot-shaper-indic-table.cc",
        "src/hb-ot-shaper-indic.cc",
        "src/hb-ot-shaper-khmer.cc",
        "src/hb-ot-shaper-myanmar.cc",
        "src/hb-ot-shaper-syllabic.cc",
        "src/hb-ot-shaper-thai.cc",
        "src/hb-ot-shaper-use.cc",
        "src/hb-ot-shaper-vowel-constraints.cc",
        "src/hb-ot-shape-fallback.cc",
        "src/hb-ot-shape-normalize.cc",
        "src/hb-ot-shape.cc",
        "src/hb-ot-tag.cc",
        "src/hb-ot-var.cc",
        "src/hb-outline.cc",
        "src/hb-paint-bounded.cc",
        "src/hb-paint-extents.cc",
        "src/hb-paint.cc",
        "src/hb-set.cc",
        "src/hb-shape-plan.cc",
        "src/hb-shape.cc",
        "src/hb-shaper.cc",
        "src/hb-static.cc",
        "src/hb-style.cc",
        "src/hb-subset-cff-common.cc",
        "src/hb-subset-cff1.cc",
        "src/hb-subset-cff2.cc",
        "src/hb-subset-input.cc",
        "src/hb-subset-instancer-iup.cc",
        "src/hb-subset-instancer-solver.cc",
        "src/hb-subset-plan.cc",
        "src/hb-subset-serialize.cc",
        "src/hb-subset.cc",
        "src/hb-ucd.cc",
        "src/hb-unicode.cc",
        # "src/hb-uniscribe.cc",
        "src/OT/Var/VARC/VARC.cc",
    ]

    if freetype_enabled:
        thirdparty_sources += [
            "src/hb-ft.cc",
        ]
        if graphite_enabled:
            thirdparty_sources += [
                "src/hb-graphite2.cc",
            ]
    if icu_enabled:
        thirdparty_sources += [
            "src/hb-icu.cc"
        ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env_harfbuzz.Prepend(CPPPATH=["#thirdparty/harfbuzz/src"])

    if icu_enabled:
        env_harfbuzz.Append(CPPDEFINES=["HAVE_ICU"])
        if env["builtin_icu4c"]:
            env_harfbuzz.Prepend(CPPPATH=["#thirdparty/icu4c/common/", "#thirdparty/icu4c/i18n/"])
            env_harfbuzz.Append(
                CPPDEFINES=[
                    "U_STATIC_IMPLEMENTATION",
                    ("U_HAVE_LIB_SUFFIX", 1),
                    ("U_LIB_SUFFIX_C_NAME", "_godot"),
                    "HAVE_ICU_BUILTIN",
                ]
            )

    if freetype_enabled:
        env_harfbuzz.Append(CPPDEFINES=["HAVE_FREETYPE"])
        if graphite_enabled:
            env_harfbuzz.Append(CPPDEFINES=["HAVE_GRAPHITE2"])
        if env["builtin_freetype"]:
            env_harfbuzz.Prepend(CPPPATH=["#thirdparty/freetype/include"])
        if env["builtin_graphite"] and graphite_enabled:
            env_harfbuzz.Prepend(CPPPATH=["#thirdparty/graphite/include"])
            env_harfbuzz.Append(CPPDEFINES=["GRAPHITE2_STATIC"])
    if env["platform"] in ["android", "linuxbsd", "web"]:
        env_harfbuzz.Append(CPPDEFINES=["HAVE_PTHREAD"])

    lib = env_harfbuzz.add_library("harfbuzz_builtin", thirdparty_sources)
    thirdparty_obj += lib

    # Needs to be appended to arrive after libscene in the linker call,
    # but we don't want it to arrive *after* system libs, so manual hack
    # LIBS contains first SCons Library objects ("SCons.Node.FS.File object")
    # and then plain strings for system library. We insert between the two.
    inserted = False
    for idx, linklib in enumerate(env["LIBS"]):
        if isinstance(linklib, (str, bytes)):  # first system lib such as "X11", otherwise SCons lib object
            env["LIBS"].insert(idx, lib)
            inserted = True
            break
    if not inserted:
        env.Append(LIBS=[lib])

# Godot source files

module_obj = []

env_harfbuzz.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
